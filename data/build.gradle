apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
//apply plugin: 'org.jmailen.kotlinter' TODO Enable when the crash is fixed
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion
    defaultConfig {
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        buildConfigField "String", "CLIENT_VERSION", "\"7.3.1\""
    }
    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "8g"
    }
    lintOptions {
        abortOnError true
        absolutePaths true
        checkAllWarnings true
        htmlOutput = new File("$rootProject.ext.staticAnalysisReportTarget/$project.name/lint.html")
        quiet false
        warningsAsErrors true
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    publishNonDefault true
    sourceSets.forEach {
        it.java.srcDirs += ["src/$it.name/kotlin"]
    }
}
repositories {
    jcenter()
}
dependencies {
    compile project(':domain')
    kapt rootProject.ext.annotationProcessorDataDependencies
    debugCompile rootProject.ext.debugCompileDataDependencies
    compile rootProject.ext.compileDependencies
    rootProject.ext.compileDataDependencies.forEach {
        compile(it) {
            // Correct RxJava is brought by the domain module
            exclude group: 'io.reactivex'
        }
    }
    provided rootProject.ext.providedDataDependencies
}
