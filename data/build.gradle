apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jmailen.kotlinter'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion
    defaultConfig {
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        buildConfigField "String", "PLATFORM_ANDROID", "\"android\""
        buildConfigField "String", "TINDER_VERSION_NAME", "\"7.4.0\""
        buildConfigField "String", "TINDER_VERSION_CODE", "\"2195\""
        buildConfigField "String", "TINDER_API_LOCALE", "\"en\""
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "${rootProject.projectDir}/assets/db".toString()
                ]
            }
        }
    }
    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "8g"
    }
    lintOptions {
        abortOnError true
        absolutePaths true
        checkAllWarnings true
        htmlOutput = new File("$rootProject.ext.staticAnalysisReportTarget/$project.name/lint.html")
        quiet false
        warningsAsErrors true
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    publishNonDefault true
    sourceSets.forEach {
        it.java.srcDirs += ["src/$it.name/kotlin"]
    }
}
repositories {
    jcenter()
    maven { url "https://maven.google.com" }
}
dependencies {
    debugCompile project(path: ':domain', configuration: 'debug')
    releaseCompile project(path: ':domain', configuration: 'release')
    debugCompile project(path: ':crash-reporter', configuration: 'debug')
    releaseCompile project(path: ':crash-reporter', configuration: 'release')
    kapt rootProject.ext.annotationProcessorDataDependencies
    debugCompile rootProject.ext.debugCompileDataDependencies
    compile rootProject.ext.compileDependencies
    rootProject.ext.compileDataDependencies.forEach {
        compile(it) {
            // Correct RxJava is brought by the domain module
            exclude group: 'io.reactivex'
        }
    }
    provided rootProject.ext.providedDataDependencies
}
